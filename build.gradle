buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven/'
        }
        maven {
            name = 'oss-sonatype-public'
            url = 'https://oss.sonatype.org/content/repositories/public/'
        }
        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
        classpath "net.minecraftforge.gradle:ForgeGradle:${project.forgeGradleVersion}"
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0'
        classpath 'org.spongepowered:mixingradle:0.4+'
        classpath 'org.spongepowered:spongegradle:0.5.+'
    }
}

// Load MalisisCore Gradle project first
evaluationDependsOn ':MalisisCore'

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.spongepowered.gradle'
apply plugin: 'org.spongepowered.plugin'

defaultTasks 'clean', 'licenseFormat', 'sortAccessTransformers', 'sortClassFields', 'build'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

archivesBaseName = project.name.toLowerCase()
version = "${project.minecraftVersion}-${project.forgeBuild}-r${project.spongeAPIVersion.split("-")[0]}"

sourceSets {
    main {
        ext.refMap = "mixins.${project.name.toLowerCase()}.refmap.json"
    }
}

minecraft {
    version = "${project.forgeVersion}"
    mappings = project.mcpMappings
    runDir = 'run'
    useDepAts = true

    atSource project(':MalisisCore').sourceSets.main

    coreMod = project.coreMod

    // We don't use the obfuscated dev jar currently
    makeObfSourceJar = false
}

deobfMcMCP { failOnAtError = false }
deobfMcSRG { failOnAtError = false }

repositories {
	mavenLocal()
    mavenCentral()
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven/'
    }
    maven {
        name = 'oss-sonatype-public'
        url = 'https://oss.sonatype.org/content/repositories/public/'
    }
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
}

allprojects {
    // Fix conflicts with HOCON library (SF requires 1.3.0)
    configurations.forgeGradleMcDeps {
        exclude group: 'com.typesafe', module: 'config'
    }
}

dependencies {
    compile "org.spongepowered:spongeforge:${project.spongeImplVersion}:dev"
    compile 'ninja.leaping.configurate:configurate-json:3.3'
    compile project(':MalisisCore')
}

idea {
    module {
        inheritOutputDirs = true
    }
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.url = project.url
    header rootProject.file('HEADER.txt')
    ignoreFailures true
    strictCheck true
    include '**/*.java'
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

sortClassFields {
    add 'main', 'com.almuradev.almura.content.block.sound.BlockSoundGroups'
    add 'main', 'com.almuradev.almura.content.item.group.ItemGroups'
    add 'main', 'com.almuradev.almura.content.material.MapColors'
    add 'main', 'com.almuradev.almura.content.material.Materials'
}

if (JavaVersion.current().isJava8Compatible() || JavaVersion.current().isJava9Compatible()) {
    tasks.withType(Javadoc) {
        // disable the crazy super-strict doclint tool in Java 8
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

compileJava {
    options.compilerArgs += [
            '-Xlint:-processing',
    ]
    options.deprecation = true
    options.encoding = 'utf8'
}

jar {
    classifier 'base'

    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': url,
                'FMLAT': "${project.name.toLowerCase()}_at.cfg"
        )
    }
}

shadowJar {
    mainSpec.sourcePaths.clear()
    dependsOn reobfJar
    classifier ''
    exclude 'dummyThing'
    dependencies {
        include dependency('ninja.leaping.configurate:configurate-json')
    }
    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
}

artifacts {
    archives shadowJar
}
